plugins {
	id 'org.jetbrains.compose'
	alias(libs.plugins.kotlin.multiplatform)
	alias(libs.plugins.kotlin.compose.compiler)
}

import org.jetbrains.compose.desktop.application.dsl.TargetFormat

compose {
	desktop {
		application {
			mainClass = "MainKt"

			nativeDistributions {
				targetFormats(TargetFormat.Dmg, TargetFormat.Msi, TargetFormat.Deb)
				packageName = "com.sleepcamel.gduplicatefinder.app"
				packageVersion = GDuplicateFinderVersion
			}
		}
	}
}

kotlin {
	jvmToolchain(project.getProperties().getOrDefault("kotlin.jvmToolchain", 11) as Integer)

	jvm("desktop")

	sourceSets {
		commonMain.dependencies {
			implementation project(':core')
			implementation compose.components.resources
			implementation compose.foundation
			implementation compose.material3
			implementation compose.runtime
			implementation compose.ui
		}
		desktopMain.dependencies {
			implementation(compose.desktop.currentOs) {
				exclude group: "org.jetbrains.compose.material"
			}
		}
		desktopTest.dependencies {
			implementation compose.desktop.uiTestJUnit4
		}
	}
}

tasks.withType(Test) {
	if (!environment.containsKey("CI")) {
		environment("SKIKO_RENDER_API", "SOFTWARE_FAST")
	}
}