plugins {
	id 'org.jetbrains.compose'
	id 'org.jetbrains.kotlin.multiplatform'
}

import org.jetbrains.compose.desktop.application.dsl.TargetFormat

compose {
	// Compose Multiplatform 1.5.11 doesn't support Kotlin 1.9.22, remove when upgrading
	kotlinCompilerPlugin.set("1.5.7")

	desktop {
		application {
			mainClass = "MainKt"

			nativeDistributions {
				targetFormats(TargetFormat.Dmg, TargetFormat.Msi, TargetFormat.Deb)
				packageName = "com.sleepcamel.gduplicatefinder.app"
				packageVersion = GDuplicateFinderVersion
			}
		}
	}
}

kotlin {
	jvmToolchain(project.getProperties().getOrDefault("kotlin.jvmToolchain", 11) as Integer)

	jvm("desktop")

	sourceSets {
		commonMain.dependencies {
			implementation project(':core')
			implementation compose.components.resources
			implementation compose.foundation
			implementation compose.material
			implementation compose.runtime
			implementation compose.ui
		}
		desktopMain.dependencies {
			implementation compose.desktop.currentOs
		}
		desktopTest.dependencies {
			implementation compose.desktop.uiTestJUnit4
		}
	}
}
