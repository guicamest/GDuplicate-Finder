pluginManagement {
    plugins {
        id 'org.jetbrains.compose' version '1.6.11'
        id 'org.jetbrains.kotlin.plugin.compose' version '2.0.20'
    }
}

plugins {
    id("org.danilopianini.gradle-pre-commit-git-hooks") version "2.0.9"
}

rootProject.name = 'GDuplicate-Finder'

// From compose template
enableFeaturePreview("TYPESAFE_PROJECT_ACCESSORS")

include 'core', 'custom-ktlint-rules', 'app'

// See https://github.com/DanySK/gradle-pre-commit-git-hooks
gitHooks { extension ->
    extension.with {
        commitMsg { context ->
            context.with {
                conventionalCommits { conventionalCommits ->
                    conventionalCommits.with {
                        defaultTypes()
                        types("deps")
                    }
                }
            }
        }
        preCommit { context ->
            context.with {
                from(settingsDir.toPath().resolve(".hooks").resolve("pre-commit-ktlint-format").toFile())
                tasks("licenseFormat", [] as String[], true)
                tasks("clean", ["build"] as String[], true)
                appendScript {
                    'docker run --rm -v $(pwd):/repo --workdir /repo rhysd/actionlint:1.6.26 -color'
                }
            }
        }
        createHooks(true)
    }
}
