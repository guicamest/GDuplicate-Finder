plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'java-library'
    id 'jvm-test-suite'
    id 'jacoco'
    id "org.jlleitschuh.gradle.ktlint" version "11.6.1"
    id "me.champeau.jmh" version "0.7.2"
}

kotlin {
    jvmToolchain(project.getProperties().getOrDefault("kotlin.jvmToolchain", 11) as Integer)
}

version = "$GDuplicateFinderVersion"

dependencies {
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3"

    implementation 'commons-codec:commons-codec:1.16.0'

    ktlintRuleset project(":custom-ktlint-rules")
}

jacoco {
    toolVersion = "0.8.11"
}

ktlint {
    version = "1.0.1"
}

testing {
    suites {
        withType(JvmTestSuite).configureEach {
            useJUnitJupiter('5.10.0')
            dependencies {
                implementation 'org.assertj:assertj-core:3.24.2'

                implementation 'org.jetbrains.kotlin:kotlin-test'
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.3"

                implementation "com.github.marschall:memoryfilesystem:2.7.0"
                implementation "com.google.jimfs:jimfs:1.3.0"
            }
        }
        integrationTest(JvmTestSuite) {
            dependencies {
                implementation project()
                implementation "software.amazon.nio.s3:aws-java-nio-spi-for-s3:1.2.4"

                implementation "ch.pontius.nio:smb-nio:0.11.0"

                // Provides transitive vulnerable dependency maven:com.jcraft:jsch:0.1.53
                implementation "com.pastdev:jsch-nio:0.1.9"
                implementation "com.jcraft:jsch:0.1.55"
            }
        }
    }
}

tasks.named('check') {
    dependsOn(testing.suites.integrationTest)
}

jacocoTestReport {
    dependsOn test
}

java {
    withSourcesJar()
}

tasks.withType(Jar) {
    manifest {
         attributes(
             'Implementation-Title': project.name,
             'Implementation-Version': project.version,
         )
    }
}

jmh {
    def buildDir = project.layout.buildDirectory.get()
    def jfrReports = buildDir.dir('reports').file('jfr')
    profilers = ["jfr:dir=${jfrReports}"]
}