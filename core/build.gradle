plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.9.10'
    id 'java-library'
    id 'jvm-test-suite'
}

kotlin {
    jvmToolchain(11)
}

version = "$GDuplicateFinderVersion"

testing {
    suites {
        withType(JvmTestSuite).configureEach {
            useJUnitJupiter('5.10.0')
            dependencies {
                implementation 'org.assertj:assertj-core:3.24.2'

                implementation 'org.jetbrains.kotlin:kotlin-test'
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.3"

                implementation "com.github.marschall:memoryfilesystem:2.6.1"
                implementation "com.google.jimfs:jimfs:1.3.0"
            }
        }
        integrationTest(JvmTestSuite) {
            dependencies {
                implementation project()
                implementation "software.amazon.nio.s3:aws-java-nio-spi-for-s3:1.2.4"

                implementation "ch.pontius.nio:smb-nio:0.11.0"

                // Provides transitive vulnerable dependency maven:com.jcraft:jsch:0.1.53
                implementation "com.pastdev:jsch-nio:0.1.7"
                implementation "com.jcraft:jsch:0.1.55"
            }
        }
    }
}

tasks.named('check') {
    dependsOn(testing.suites.integrationTest)
}

java {
    withSourcesJar()
}

tasks.withType(Jar) {
    manifest {
         attributes(
             'Implementation-Title': project.name,
             'Implementation-Version': project.version,
         )
    }
}
